/*
 *  _____ _                       _             _     _     _
 * /  ___| |                     (_)           | |   (_)   | |
 * \ `--.| |__   ___  _ __  _ __  _ _ __   __ _| |    _ ___| |_
 *  `--. \ '_ \ / _ \| '_ \| '_ \| | '_ \ / _` | |   | / __| __|
 * /\__/ / | | | (_) | |_) | |_) | | | | | (_| | |___| \__ \ |_
 * \____/|_| |_|\___/| .__/| .__/|_|_| |_|\__, \_____/_|___/\__|
 *                   | |   | |             __/ |
 *                   |_|   |_|            |___/
 *
 * Copyright (C) 2021-2021  Sebastian GÃ¶ls
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 2 of the License only
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, write to the Free Software Foundation, Inc.,
 * 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
 */

plugins {
	id "com.android.application"
	id "kotlin-android"
	id "kotlin-android-extensions"
	id "kotlin-kapt"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
	compileSdkVersion 31
	buildToolsVersion "30.0.3"

	defaultConfig {
		applicationId "pl.edu.pjwstk.s999844.shoppinglist"
		minSdkVersion 24
		targetSdkVersion 31
		versionCode 8
		versionName "v1.7.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments["room.incremental"] = "true"
			}
		}
	}
	signingConfigs {
		release {
			storeFile file(keystoreProperties["storeFile"])
			storePassword keystoreProperties["storePassword"]
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
			v2SigningEnabled true
		}
	}
	applicationVariants.all {
		variant -> variant.resValue "string", "versionName", variant.versionName
	}
	buildTypes {
		release {
			minifyEnabled true
			signingConfig signingConfigs.release
			proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = "1.8"
	}
	buildFeatures {
		viewBinding = true
	}
	lintOptions {
		abortOnError true
		warningsAsErrors true
		checkAllWarnings true
		showAll true
		disable "GradleDependency", "NewerVersionAvailable", "DuplicateStrings", "GoogleAppIndexingApiWarning", "MissingTranslation"
	}
}

dependencies {
	implementation "androidx.lifecycle:lifecycle-process:2.3.0"

	def androidx_navigation_version = "2.3.5"
	implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
	implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"

	def room_version = "2.2.6"
	implementation "androidx.room:room-runtime:$room_version"
	kapt "androidx.room:room-compiler:$room_version"
	implementation fileTree(include: ["*.jar"], dir: "libs")

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "androidx.core:core-ktx:1.3.2"
	implementation "androidx.appcompat:appcompat:1.2.0"
	implementation "com.google.android.material:material:1.3.0"
	implementation "androidx.constraintlayout:constraintlayout:2.0.4"

	testImplementation "junit:junit:4.13.1"
	androidTestImplementation "androidx.test.ext:junit:1.1.2"
	androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}

